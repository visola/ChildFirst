buildscript {
  repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.moowork.gradle:gradle-node-plugin:0.12"
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.apache.httpcomponents:httpclient:4.3.6'
  compile 'org.springframework.boot:spring-boot-starter-data-jpa'
  compile 'org.springframework.boot:spring-boot-starter-security'
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'org.visola.spring.security:spring-security-token-filter-spring-boot-starter:1.1'

  runtime 'com.zaxxer:HikariCP'
  runtime 'mysql:mysql-connector-java:5.1.38'
  runtime 'org.liquibase:liquibase-core'
  runtime 'org.springframework.boot:spring-boot-starter-thymeleaf'
}

sourceSets {
  main {
    resources {
      srcDir "${buildDir}/frontend"
    }
  }
}

bootRun {
  addResources = false
}

task bundleFrontend (type: NpmTask) {
  args = ['run', 'bundleProd']
  inputs.dir file("src/frontend")
  outputs.dir file("${buildDir}/frontend/static")
}

processResources.dependsOn bundleFrontend
