buildscript {
  repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.moowork.gradle:gradle-node-plugin:0.12"
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE'
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.apache.httpcomponents:httpclient:4.3.6'
  compile 'org.springframework.boot:spring-boot-starter-data-jpa'
  compile 'org.springframework.boot:spring-boot-starter-security'
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'org.visola.spring.security:spring-security-token-filter-spring-boot-starter:1.1'

  runtime 'com.zaxxer:HikariCP'
  runtime 'mysql:mysql-connector-java:5.1.38'
  runtime 'org.liquibase:liquibase-core'
  runtime 'org.springframework.boot:spring-boot-starter-thymeleaf'
}

sourceSets {
  main {
    resources {
      srcDir "${buildDir}/node"
      srcDir "${buildDir}/node_modules"
    }
  }
}

bootRun {
  addResources = false
}

task copyNodeModules(type: Copy) {
  from "node_modules/babel-polyfill/dist/polyfill.js"
  into "${buildDir}/node_modules/static/js/lib"
}

task javaScriptTranspile (type: NpmTask) {
  dependsOn copyNodeModules
  args = ['run', 'build']
  inputs.dir file("src/main/js")
  outputs.dir file("${buildDir}/node")
}

processResources.dependsOn javaScriptTranspile
